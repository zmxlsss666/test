apply plugin: 'io.gitlab.arturbosch.detekt'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'jacoco'

// Detekt configuration
detekt {
    toolVersion = "1.23.3"
    config = files("$projectDir/config/detekt/detekt.yml")
    buildUponDefaultConfig = true
    autoCorrect = true
}

// KtLint configuration
ktlint {
    android = true
    ignoreFailures = false
    reporters {
        reporter "plain"
        reporter "checkstyle"
        reporter "html"
    }
}

// JaCoCo configuration
jacoco {
    toolVersion = "0.8.8"
}

tasks.withType(Test) {
    jacoco {
        excludeClassLoaders = ["*JarClassLoader"]
        includeNoLocationClasses = true
    }
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {
    group = 'Reporting'
    description = 'Generate Jacoco coverage reports'

    reports {
        xml.required.set(true)
        html.required.set(true)
    }

    def fileFilter = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
            '**/*_Impl.*',
            '**/*_Factory.*',
            '**/*_Provide*Factory*.*',
            '**/*Dagger*.*',
            '**/*Hilt*.*',
            '**/*_MembersInjector.*'
    ]
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec',
            'outputs/code_coverage/debugAndroidTest/connected/**/*.ec'
    ]))
}
